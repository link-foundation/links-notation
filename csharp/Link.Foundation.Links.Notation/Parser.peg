@namespace Link.Foundation.Links.Notation
@classname Parser
@using System.Linq
document <IList<Link<string>>> = #{ state["IndentationStack"] = new Stack<int>(); state["IndentationStack"].Push(0); state["BaseIndentation"] = null; } skipEmptyLines l:links _ eof { l.ToLinksList() } / #{ state["IndentationStack"] = new Stack<int>(); state["IndentationStack"].Push(0); state["BaseIndentation"] = null; } _ eof { new List<Link<string>>() }
skipEmptyLines = ([ \t]* [\r\n])*
links <IList<LinksGroup<string>>> = fl:firstLine list:line* POP_INDENTATION { new List<LinksGroup<string>> { fl }.Concat(list).ToList() }
firstLine <LinksGroup<string>> = SET_BASE_INDENTATION l:element { l }
line <LinksGroup<string>> = CHECK_INDENTATION l:element { l }
element <LinksGroup<string>> = e:anyLink PUSH_INDENTATION l:links { new LinksGroup<string>(e, l) } / e:anyLink { new LinksGroup<string>(e) }
referenceOrLink <Link<string>> = l:multiLineAnyLink { l } / i:reference { i }
anyLink <Link<string>> = ml:multiLineAnyLink eol { ml } / il:indentedIdLink { il } / sl:singleLineAnyLink { sl }
multiLineAnyLink <Link<string>> = multiLineValueLink / multiLineLink
singleLineAnyLink <Link<string>> = fl:singleLineLink eol { fl } / vl:singleLineValueLink eol { vl }
multiLineValueAndWhitespace <Link<string>> = value:referenceOrLink _ { value }
multiLineValues <IList<Link<string>>> = _ list:multiLineValueAndWhitespace* { list }
singleLineValueAndWhitespace <Link<string>> = __ value:referenceOrLink { value }
singleLineValues <IList<Link<string>>> = list:singleLineValueAndWhitespace+ { list }
singleLineLink <Link<string>> = __ id:(reference) __ ":" v:singleLineValues { new Link<string>(id, v) }
multiLineLink <Link<string>> = "(" _ id:(reference) _ ":" v:multiLineValues _ ")" { new Link<string>(id, v) }
singleLineValueLink <Link<string>> = v:singleLineValues { new Link<string>(v) }
multiLineValueLink <Link<string>> = "(" v:multiLineValues _ ")" { new Link<string>(v) }
indentedIdLink <Link<string>> = id:(reference) __ ":" eol { new Link<string>(id) }

reference <string> = doubleQuotedReference / singleQuotedReference / simpleReference
simpleReference <string> = "" referenceSymbol+
doubleQuotedReference <string> = '"' r:([^"]+) '"' { string.Join("", r) }
singleQuotedReference <string> = "'" r:([^']+) "'" { string.Join("", r) }
SET_BASE_INDENTATION = spaces:" "* #{ if (state["BaseIndentation"] == null) { state["BaseIndentation"] = spaces.Count; } }
PUSH_INDENTATION = spaces:" "* &{ int baseInd = state["BaseIndentation"] != null ? (int)state["BaseIndentation"] : 0; int normalized = Math.Max(0, spaces.Count - baseInd); return normalized > state["IndentationStack"].Peek(); } #{ int baseInd = state["BaseIndentation"] != null ? (int)state["BaseIndentation"] : 0; int normalized = Math.Max(0, spaces.Count - baseInd); state["IndentationStack"].Push(normalized); }
POP_INDENTATION = #{ state["IndentationStack"].Pop(); }
CHECK_INDENTATION = spaces:" "* &{ int baseInd = state["BaseIndentation"] != null ? (int)state["BaseIndentation"] : 0; int normalized = Math.Max(0, spaces.Count - baseInd); return normalized >= state["IndentationStack"].Peek(); }
eol = __ ("" [\r\n]+ / eof)
eof = !.
__ = [ \t]*
_ = whiteSpaceSymbol*
whiteSpaceSymbol = [ \t\n\r]
referenceSymbol = [^ \t\n\r(:)]
